plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'eclipse'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
    id 'xyz.jpenilla.run-velocity' version '2.3.1'
}

group = 'com.us3rn1me'
version = project.findProperty('pluginVersion') ?: 'dev'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'spigot-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'tcoded-releases'
        url = 'https://repo.tcoded.com/releases'
    }
}

dependencies {
    // Velocity
    compileOnly 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'

    // Bukkit / Spigot â€” compile against 1.8.8 to keep full 1.7-1.21.x compatibility
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'

    // Shaded
    implementation 'com.electronwill.night-config:toml:3.6.7'
    implementation 'com.tcoded:FoliaLib:0.5.1'
    implementation 'com.github.cryptomorin:XSeries:13.0.0'
}

tasks {
    shadowJar {
        archiveClassifier.set('')
        relocate 'com.electronwill.nightconfig', 'com.us3rn1me.noVPN.libs.nightconfig'
        relocate 'com.tcoded.folialib',           'com.us3rn1me.noVPN.libs.folialib'
        relocate 'com.cryptomorin.xseries',       'com.us3rn1me.noVPN.libs.xseries'
    }

    build {
        dependsOn shadowJar
    }

    runVelocity {
        velocityVersion '3.4.0-SNAPSHOT'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = ['version': project.version]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

project.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
